#!/usr/bin/env raku
use v6.d;

use ML::Spellchecker;

my %*SUB-MAIN-OPTS = :named-anywhere;

multi format-output(Str $format, @output) {
    given $format.lc {
        when $_ eq 'raku' && @output.elems == 1 { say @output[0].raku; }
        when @output.elems == 1                 { say @output[0]; }
        when $_ (elem) <string text>            { say @output.join(' '); }
        when $_ (elem) <lines>                  { .say for @output; }
        default                                 { say @output.raku; }
    }
}

#| Spellchecks words in text.
multi sub MAIN(Str $text,                     #= Text to spilt and its words stemmed.
               Str :$splitter = '\W+',        #= String to make a split regex with.
               Str :$method = 'simple',       #= Which method to use.
               Bool :p(:$pairs) = False,      #= Should pairs be returned or not?
               Str :$format= 'text'           #= Output format one of 'text', 'lines', or 'raku'.
               ) {
    my @res = word-correction($text.split(/<{ $splitter }>/), :$method, :$pairs);
    format-output($format, @res);
}

#| Spellchecks words.
multi sub MAIN(*@words,                       #= Words to be stemmed.
               Str :$method = 'simple',       #= Which method to use.
               Bool :p(:$pairs) = False,      #= Should pairs be returned or not?
               Str :$format= 'text'           #= Output format one of 'text', 'lines', or 'raku'.
               ) {
    format-output($format, word-correction(@words, :$method, :$pairs))
}

#| Spellchecks words in (pipeline) input.
multi sub MAIN(
        Str :$method = 'simple',       #= Which method to use.
        Bool :p(:$pairs) = False,      #= Should pairs be returned or not?
        Str :$format= 'text'           #= Output format one of 'text', 'lines', or 'raku'.
               ) {
    my @words = lines.join(' ').words;
    format-output($format, word-correction(@words, :$method, :$pairs))
}